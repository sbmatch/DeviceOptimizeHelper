plugins {
    id 'com.android.application'
}

android {
    namespace 'ma.DeviceOptimizeHelper'
    compileSdk 34

    defaultConfig {
        applicationId "ma.DeviceOptimizeHelper"
        minSdk 26
        targetSdk 34
        versionCode 220
        versionName "2.2.0"

        // testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            versionNameSuffix getCommitId()
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }



    buildFeatures {
        aidl true
        buildConfig true
    }

    applicationVariants.all { variant ->
        // 遍历所有的变体
        variant.outputs.all {
            // 获取应用的版本名
            def versionName = variant.versionName
            // 设置输出文件名
            outputFileName = "deviceopt_v${versionName}.apk"
        }
    }

}

def getCommitId() {
    try {
        def command = "git rev-parse --short HEAD".execute()
        command.waitFor()
        return "_" + command.text.trim()
    } catch (Exception e) {
        return "unknown"
    }
}

configurations.configureEach {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.22"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.22"

    implementation 'androidx.activity:activity:1.9.1'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'
    implementation "io.github.iamr0s:Dhizuku-API:2.5.3"
    implementation 'commons-net:commons-net:3.8.0'
    implementation 'com.tencent:mmkv:1.3.9'
    implementation 'com.google.android.material:material:1.7.0'

    def shizuku_version = ("13.1.5")
    implementation "dev.rikka.shizuku:api:$shizuku_version"
    implementation "dev.rikka.shizuku:provider:$shizuku_version"
    implementation "dev.rikka.rikkax.appcompat:appcompat:1.6.1"
    implementation "dev.rikka.rikkax.compatibility:compatibility:2.0.0"
    implementation 'dev.rikka.rikkax.material:material-preference:2.0.0'
    implementation 'dev.rikka.rikkax.preference:simplemenu-preference:1.0.3'
    implementation "dev.rikka.rikkax.lifecycle:lifecycle-viewmodel-lazy:2.0.0"
    implementation 'dev.rikka.rikkax.widget:mainswitchbar:1.1.0'
    implementation 'dev.rikka.rikkax.material:material:2.7.0'
}